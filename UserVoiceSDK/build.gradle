buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.6.+'
    }
}

apply plugin: 'android-library'

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 18
    buildToolsVersion "18.1.0"

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 18
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }
}

dependencies {
    compile 'com.android.support:support-v4:18.+@jar'
    compile 'com.android.support:appcompat-v7:18.+@aar'
    compile 'oauth.signpost:signpost-commonshttp4:1.2.1.2@jar'
    compile 'oauth.signpost:signpost-core:1.2.1.2@jar'
    compile 'org.eclipse.birt.runtime.3_7_1:org.apache.commons.codec:1.3.0@jar'
}

// Publish to maven
apply plugin: 'maven'

configurations {
    archives {
        extendsFrom configurations.default
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file://$buildDir/mvnRepo/")
            pom.project {
                name 'UserVoice Android SDK Library'
                artifactId 'uservoice-android-sdk'
                packaging 'aar'
                description 'UserVoice for Android allows you to embed UserVoice directly in your Android apps Android applications'
                url 'https://github.com/uservoice/uservoice-android-sdk'
                inceptionYear '2013'

                scm {
                    url 'scm:git@github.com:uservoice/uservoice-android-sdk.git'
                    connection 'scm:git@github.com:uservoice/uservoice-android-sdk.git'
                    developerConnection 'scm:git@github.com:uservoice/uservoice-android-sdk.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'austintaylor'
                        name 'Austin Taylor'
                        email 'austin.taylor@gmail.com'
                    }
                }
            }
        }
    }
}

